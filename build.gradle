buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://repo.spongepowered.org/maven' }
        maven { url = 'https://maven.parchmentmc.org' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
        classpath 'org.parchmentmc:librarian:1.+'
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'maven-publish'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'org.parchmentmc.librarian.forgegradle'

version = "1.3.3-modified-1.18.2"
group = "com.mrcrayfish"
archivesBaseName = "gun-mod"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

sourceSets.main.resources { srcDir 'src/generated/resources' }

mixin {
    add sourceSets.main, "cgm.refmap.json"
}

minecraft {
    mappings channel: 'parchment', version: "${mc_version}-${parchment_version}"
    // mappings channel: 'official', version: '1.18.2'

    runs {
        client {
            workingDirectory project.file('run')
            property 'mixin.env.disableRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            arg '-mixin.config=cgm.mixins.json'
            mods {
                cgm {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run/SERVER')
            property 'mixin.env.disableRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            arg '-mixin.config=cgm.mixins.json'
            arg '-nogui'
            mods {
                cgm {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'mixin.env.disableRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'cgm', '--all', '--output', file('src/generated/resources/')
            arg '-mixin.config=cgm.mixins.json'
            mods {
                cgm {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    mavenLocal()
    maven {
        url "https://www.cursemaven.com"
    }
    maven {
        name "Progwml6 maven"
        url "https://dvs1.progwml6.com/files/maven/"
    }
    /*maven {
        name "ModMaven"
        url "https://modmaven.k-4u.nl"
    }*/
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.18.2-40.2.0'
    implementation fg.deobf('curse.maven:framework-549225:4287475')
    implementation fg.deobf('curse.maven:configured-457570:4010624')
    implementation fg.deobf('curse.maven:catalogue-459701:3803098')
    implementation fg.deobf('curse.maven:backpacked-352835:3923041')
    compileOnly fg.deobf('curse.maven:controllable-317269:4120618')
    compileOnly fg.deobf("mezz.jei:jei-1.18.2:9.7.1.255:api")
    runtimeOnly fg.deobf("mezz.jei:jei-1.18.2:9.7.1.255")
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    runtimeOnly fg.deobf("curse.maven:stabx-modern-guns-690674:4535375")
}

jar {
    manifest {
        attributes([
            "Specification-Title": "Gun Mod",
            "Specification-Vendor": "MrCrayfish (modified by En0ri4n)",
            "Specification-Version": "1",
            "Implementation-Title": "Gun Mod",
            "Implementation-Version": project.version,
            "Implementation-Vendor" : "MrCrayfish (modified by En0ri4n)",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "MixinConfigs": "cgm.mixins.json"
        ])
    }
}

tasks.register('sourceJar', Jar) {
    dependsOn tasks.classes
    from sourceSets.main.allSource
    classifier 'sources'
    extension 'jar'
    group 'build'
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifacts {
                artifact jar
                artifact tasks.sourceJar
            }
        }
    }
    repositories {
        maven {
            url "file:///X:/localmaven/mcmods"
        }
    }
}
